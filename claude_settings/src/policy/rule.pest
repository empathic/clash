// Grammar for policy rule strings.
//
// Format: effect entity tool pattern
// Example: permit * bash cargo build *

rule = { SOI ~ effect ~ sep ~ entity ~ sep ~ tool ~ sep ~ pattern ~ EOI }

effect = { "permit" | "forbid" | "ask" | "delegate" }

entity = { negation? ~ entity_value }
negation = { "!" }
entity_value = { wildcard | typed_entity | identifier }
typed_entity = { identifier ~ ":" ~ (wildcard | identifier) }
wildcard = { "*" }
identifier = @{ (ASCII_ALPHANUMERIC | "_" | "-" | ".")+ }

tool = { "bash" | "read" | "write" | "edit" | "*" }

pattern = @{ ANY+ }

sep = _{ (" " | "\t")+ }
